
Процедура Подключаемый_ам_ДопПоказателиРасчет(Запрос, Стр, Индекс, Параметры) Экспорт
	
	Если Стр.Ресурс="ОтицательныйОстаток" Тогда
		ТекстРесурса = "СУММА(Рег.КоличествоОстаток)";
	ИначеЕсли Стр.Ресурс="СуммаОстатокПоУчетнымЦенам" Тогда
		ТекстРесурса = "СУММА(ВЫРАЗИТЬ(Рег.КоличествоОстаток * УчетныеЦены.Цена КАК ЧИСЛО(15, 2)))";
	ИначеЕсли Стр.Ресурс="КоличествоОстатокПоЗаказамПоставщикам" Тогда
		ТекстРесурса = "СУММА(Рег.КоличествоОстаток)";
	ИначеЕсли Стр.Ресурс="СуммаОстатокПоЗаказамПоставщикам" Тогда
		ТекстРесурса = "СУММА(ЕСТЬNULL(ЗаказПоставщикуЗапасы.Цена, 0) * Рег.КоличествоОстаток)";
	ИначеЕсли Стр.Ресурс="КоличествоЧековВозврат" Тогда
		ТекстРесурса = "СУММА(Рег.КоличествоЧековВозврат)";
	ИначеЕсли Стр.Ресурс="КоличествоЧековПродажа" Тогда
		ТекстРесурса = "СУММА(Рег.КоличествоЧековПродажа)";
	ИначеЕсли Стр.Ресурс="КоличествоЧековПредоплата" Тогда
		ТекстРесурса = "КОЛИЧЕСТВО(РАЗЛИЧНЫЕ Рег.Документ)";
	ИначеЕсли Стр.Ресурс="СуммаЧековПредоплата" Тогда
		ТекстРесурса = "СУММА(Рег.Сумма)";
	ИначеЕсли Стр.Ресурс="СуммаЧековВозврат" Тогда
		ТекстРесурса = "СУММА(Рег.СуммаВозврат)";
	ИначеЕсли Стр.Ресурс="СуммаЧековПродажа" Тогда
		ТекстРесурса = "СУММА(Рег.СуммаВыручки)+СУММА(Рег.СуммаВозврат)";
	ИначеЕсли Стр.Ресурс="СреднийЧекПоПродажам" Тогда
		ТекстРесурса = "ВЫБОР КОГДА СУММА(Рег.КоличествоЧековПродажа) = 0 ТОГДА 0 ИНАЧЕ (СУММА(Рег.СуммаВыручки) + СУММА(Рег.СуммаВозврат)) / СУММА(Рег.КоличествоЧековПродажа)	КОНЕЦ";
	ИначеЕсли Стр.Ресурс="ПоступлениеНаличныхВКассыККМ" Тогда
		ТекстРесурса = "СУММА(Рег.Сумма)";
	ИначеЕсли Стр.Ресурс="ВыбытиеНаличныхИзКассыККМ" Тогда
		ТекстРесурса = "СУММА(Рег.Сумма)";
	ИначеЕсли Стр.Ресурс="ПолученоОплатКартой" Тогда
		ТекстРесурса = "СУММА(Рег.Приход)";
	ИначеЕсли Стр.Ресурс="ВозвратОплатКартой" Тогда
		ТекстРесурса = "СУММА(Рег.Расход)";
	ИначеЕсли Стр.Ресурс="ПередачаНаличныхВСейф" Тогда
		ТекстРесурса = "СУММА(Рег.СуммаОстаток)";
	ИначеЕсли Стр.Ресурс="ВыдачаНаличныхИзСейфа" Тогда
		ТекстРесурса = "СУММА(Рег.Сумма)";
	Иначе
		Возврат;
	КонецЕсли; 
	
	Если Стр.Ресурс="ОтицательныйОстаток" Тогда
		ТекстЭлемента =  
		"ВЫБРАТЬ
		|	&ДляСравнения КАК ДляСравнения,
		|	&Индекс КАК Индекс,
		|	&Разделитель КАК Разделитель,
		|	&Ресурс КАК Значение
		|ИЗ
		|	(ВЫБРАТЬ
		|		СУММА(Рег.КоличествоОстаток) КАК КоличествоОстаток,
		|		Рег.Номенклатура КАК Номенклатура,
		|		Рег.СтруктурнаяЕдиница КАК СтруктурнаяЕдиница
		|	ИЗ
		|		РегистрНакопления.Запасы.Остатки(&Период, СчетУчета.ТипСчета = ЗНАЧЕНИЕ(Перечисление.ТипыСчетов.Запасы)) КАК Рег
		|	ГДЕ
		|		Рег.КоличествоОстаток < 0
		|	
		|	СГРУППИРОВАТЬ ПО
		|		Рег.Номенклатура,
		|		Рег.СтруктурнаяЕдиница) КАК Рег
		|СГРУППИРОВАТЬ ПО &Группировки";
	ИначеЕсли Стр.Ресурс="СуммаОстатокПоУчетнымЦенам" Тогда
		ТекстЭлемента =  
		"ВЫБРАТЬ
		|   &ДляСравнения КАК ДляСравнения, 
		|   &Индекс КАК Индекс, 
		|   &Разделитель КАК Разделитель, 
		|	&Ресурс КАК Значение
		|ИЗ
		|	РегистрНакопления.Запасы.Остатки(&Период, СчетУчета.ТипСчета = ЗНАЧЕНИЕ(Перечисление.ТипыСчетов.Запасы)) КАК Рег
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ЦеныНоменклатуры.СрезПоследних(&Период, ВидЦен = ЗНАЧЕНИЕ(Справочник.ВидыЦен.Учетная)) КАК УчетныеЦены
		|		ПО Рег.Номенклатура = УчетныеЦены.Номенклатура
		|			И Рег.Характеристика = УчетныеЦены.Характеристика
		|ГДЕ &Отборы
		|СГРУППИРОВАТЬ ПО &Группировки";
	ИначеЕсли Стр.Ресурс="КоличествоОстатокПоЗаказамПоставщикам"
		ИЛИ Стр.Ресурс="СуммаОстатокПоЗаказамПоставщикам" Тогда
		ТекстЭлемента =  
		"ВЫБРАТЬ
		|   &ДляСравнения КАК ДляСравнения, 
		|   &Индекс КАК Индекс, 
		|   &Разделитель КАК Разделитель, 
		|	&Ресурс КАК Значение
		|ИЗ
		|	РегистрНакопления.ЗаказыПоставщикам.Остатки(&Период, ) КАК Рег
		|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ЗаказПоставщику.Запасы КАК ЗаказПоставщикуЗапасы
		|		ПО Рег.ЗаказПоставщику = ЗаказПоставщикуЗапасы.Ссылка
		|			И Рег.Номенклатура = ЗаказПоставщикуЗапасы.Номенклатура
		|			И Рег.Характеристика = ЗаказПоставщикуЗапасы.Характеристика
		|ГДЕ &Отборы
		|СГРУППИРОВАТЬ ПО &Группировки";
	ИначеЕсли Стр.Ресурс="КоличествоЧековПродажа" ИЛИ Стр.Ресурс="КоличествоЧековВозврат" 
		ИЛИ Стр.Ресурс="СуммаЧековПродажа" ИЛИ Стр.Ресурс="СуммаЧековВозврат" ИЛИ Стр.Ресурс="СреднийЧекПоПродажам" Тогда
		ТекстЭлемента = 
		"ВЫБРАТЬ
		|	&ДляСравнения КАК ДляСравнения,
		|	&Индекс КАК Индекс,
		|	Рег.Разделитель КАК Разделитель,
		|	&Ресурс КАК Значение
		|ИЗ
		|	(ВЫБРАТЬ
		|		&Разделитель КАК Разделитель,
		|		ПродажиОбороты.Документ.КассаККМ КАК КассаККМ,
		|		ПродажиОбороты.Организация КАК Организация,
		|		ПродажиОбороты.Документ.Контрагент КАК Контрагент,
		|		ПродажиОбороты.Подразделение КАК Подразделение,
		|		ПродажиОбороты.Документ.СтруктурнаяЕдиница КАК Склад,
		|		КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ВЫБОР
		|				КОГДА ПродажиОбороты.Регистратор ССЫЛКА Документ.ЧекККМ
		|						ИЛИ ПродажиОбороты.Регистратор ССЫЛКА Документ.ЧекККМВозврат
		|					ТОГДА ПродажиОбороты.Регистратор
		|				КОГДА ДокументЧекККМ.Ссылка ССЫЛКА Документ.ЧекККМ
		|					ТОГДА ДокументЧекККМ.Ссылка
		|				КОГДА ДокументЧекККМВозврат.Ссылка ССЫЛКА Документ.ЧекККМВозврат
		|					ТОГДА ДокументЧекККМВозврат.Ссылка
		|			КОНЕЦ) КАК КоличествоЧеков,
		|		0 КАК КоличествоЧековПродажа,
		|		0 КАК КоличествоЧековВозврат,
		|		0 КАК СуммаВыручки,
		|		0 КАК СуммаВозврат
		|	ИЗ
		|		РегистрНакопления.Продажи.Обороты(&ДатаНачала, &ДатаОкончания, Авто, ) КАК ПродажиОбороты
		|			ЛЕВОЕ СОЕДИНЕНИЕ Документ.ЧекККМ КАК ДокументЧекККМ
		|			ПО ПродажиОбороты.Регистратор = ДокументЧекККМ.КассоваяСмена
		|			ЛЕВОЕ СОЕДИНЕНИЕ Документ.ЧекККМВозврат КАК ДокументЧекККМВозврат
		|			ПО ПродажиОбороты.Регистратор = ДокументЧекККМВозврат.КассоваяСмена
		|	ГДЕ
		|		(ПродажиОбороты.Документ ССЫЛКА Документ.ОтчетОРозничныхПродажах
		|				ИЛИ ПродажиОбороты.Документ ССЫЛКА Документ.ЧекККМ)
		|		И ПродажиОбороты.КоличествоОборот <> 0
		|	
		|	СГРУППИРОВАТЬ ПО
		|		ПродажиОбороты.Подразделение,
		|		ПродажиОбороты.Документ.КассаККМ,
		|		ПродажиОбороты.Организация,
		|		ПродажиОбороты.Документ.СтруктурнаяЕдиница,
		|		ПродажиОбороты.Документ.Контрагент
		|	
		|	ОБЪЕДИНИТЬ ВСЕ
		|	
		|	ВЫБРАТЬ
		|		&Разделитель,
		|		ПродажиОбороты.Документ.КассаККМ,
		|		ПродажиОбороты.Организация,
		|		ПродажиОбороты.Документ.Контрагент,
		|		ПродажиОбороты.Подразделение,
		|		ПродажиОбороты.Документ.СтруктурнаяЕдиница,
		|		0,
		|		КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ВЫБОР
		|				КОГДА ПродажиОбороты.Регистратор ССЫЛКА Документ.ЧекККМ
		|					ТОГДА ПродажиОбороты.Регистратор
		|				КОГДА ДокументЧекККМ.Ссылка ССЫЛКА Документ.ЧекККМ
		|					ТОГДА ДокументЧекККМ.Ссылка
		|			КОНЕЦ),
		|		0,
		|		0,
		|		0
		|	ИЗ
		|		РегистрНакопления.Продажи.Обороты(&ДатаНачала, &ДатаОкончания, Авто, ) КАК ПродажиОбороты
		|			ЛЕВОЕ СОЕДИНЕНИЕ Документ.ЧекККМ КАК ДокументЧекККМ
		|			ПО ПродажиОбороты.Регистратор = ДокументЧекККМ.КассоваяСмена
		|	ГДЕ
		|		(ПродажиОбороты.Документ ССЫЛКА Документ.ОтчетОРозничныхПродажах
		|				ИЛИ ПродажиОбороты.Документ ССЫЛКА Документ.ЧекККМ)
		|		И ПродажиОбороты.КоличествоОборот <> 0
		|	
		|	СГРУППИРОВАТЬ ПО
		|		ПродажиОбороты.Подразделение,
		|		ПродажиОбороты.Документ.КассаККМ,
		|		ПродажиОбороты.Организация,
		|		ПродажиОбороты.Документ.СтруктурнаяЕдиница,
		|		ПродажиОбороты.Документ.Контрагент
		|	
		|	ОБЪЕДИНИТЬ ВСЕ
		|	
		|	ВЫБРАТЬ
		|		&Разделитель,
		|		ПродажиОбороты.Документ.КассаККМ,
		|		ПродажиОбороты.Организация,
		|		ПродажиОбороты.Документ.Контрагент,
		|		ПродажиОбороты.Подразделение,
		|		ПродажиОбороты.Документ.СтруктурнаяЕдиница,
		|		0,
		|		0,
		|		КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ВЫБОР
		|				КОГДА ПродажиОбороты.Регистратор ССЫЛКА Документ.ЧекККМВозврат
		|					ТОГДА ПродажиОбороты.Регистратор
		|				КОГДА ДокументЧекККМВозврат.Ссылка ССЫЛКА Документ.ЧекККМВозврат
		|					ТОГДА ДокументЧекККМВозврат.Ссылка
		|			КОНЕЦ),
		|		0,
		|		0
		|	ИЗ
		|		РегистрНакопления.Продажи.Обороты(&ДатаНачала, &ДатаОкончания, Авто, ) КАК ПродажиОбороты
		|			ЛЕВОЕ СОЕДИНЕНИЕ Документ.ЧекККМВозврат КАК ДокументЧекККМВозврат
		|			ПО ПродажиОбороты.Регистратор = ДокументЧекККМВозврат.КассоваяСмена
		|	ГДЕ
		|		(ПродажиОбороты.Документ ССЫЛКА Документ.ОтчетОРозничныхПродажах
		|				ИЛИ ПродажиОбороты.Документ ССЫЛКА Документ.ЧекККМ)
		|		И ПродажиОбороты.КоличествоОборот <> 0
		|	
		|	СГРУППИРОВАТЬ ПО
		|		ПродажиОбороты.Подразделение,
		|		ПродажиОбороты.Документ.КассаККМ,
		|		ПродажиОбороты.Организация,
		|		ПродажиОбороты.Документ.СтруктурнаяЕдиница,
		|		ПродажиОбороты.Документ.Контрагент
		|	
		|	ОБЪЕДИНИТЬ ВСЕ
		|	
		|	ВЫБРАТЬ
		|		&Разделитель,
		|		ПродажиОбороты.Документ.КассаККМ,
		|		ПродажиОбороты.Организация,
		|		ПродажиОбороты.Документ.Контрагент,
		|		ПродажиОбороты.Документ.Подразделение,
		|		ПродажиОбороты.Документ.СтруктурнаяЕдиница,
		|		0,
		|		0,
		|		0,
		|		СУММА(ПродажиОбороты.СуммаОборот),
		|		0
		|	ИЗ
		|		РегистрНакопления.Продажи.Обороты(&ДатаНачала, &ДатаОкончания, Авто, ) КАК ПродажиОбороты
		|	ГДЕ
		|		(ПродажиОбороты.Документ ССЫЛКА Документ.ОтчетОРозничныхПродажах
		|				ИЛИ ПродажиОбороты.Документ ССЫЛКА Документ.ЧекККМ)
		|	
		|	СГРУППИРОВАТЬ ПО
		|		ПродажиОбороты.Документ.Подразделение,
		|		ПродажиОбороты.Документ.КассаККМ,
		|		ПродажиОбороты.Документ.Контрагент,
		|		ПродажиОбороты.Организация,
		|		ПродажиОбороты.Документ.СтруктурнаяЕдиница
		|	
		|	ОБЪЕДИНИТЬ ВСЕ
		|	
		|	ВЫБРАТЬ
		|		&Разделитель,
		|		Продажи.Документ.КассаККМ,
		|		Продажи.Организация,
		|		Продажи.Документ.Контрагент,
		|		Продажи.Документ.Подразделение,
		|		Продажи.Документ.СтруктурнаяЕдиница,
		|		0,
		|		0,
		|		0,
		|		0,
		|		СУММА(-1 * Продажи.Сумма)
		|	ИЗ
		|		РегистрНакопления.Продажи КАК Продажи
		|	ГДЕ
		|		Продажи.Период МЕЖДУ &ДатаНачала И &ДатаОкончания
		|		И Продажи.Сумма < 0
		|	
		|	СГРУППИРОВАТЬ ПО
		|		Продажи.Документ.Подразделение,
		|		Продажи.Документ.КассаККМ,
		|		Продажи.Документ.Контрагент,
		|		Продажи.Организация,
		|		Продажи.Документ.СтруктурнаяЕдиница) КАК Рег
		|ГДЕ
		|	&Отборы
		|
		|СГРУППИРОВАТЬ ПО
		|	Рег.Разделитель";
	ИначеЕсли Стр.Ресурс="ПоступлениеНаличныхВКассыККМ" Тогда
		ТекстЭлемента =  
		"ВЫБРАТЬ
		|	&ДляСравнения КАК ДляСравнения,
		|	&Индекс КАК Индекс,
		|	&Разделитель КАК Разделитель,
		|	&Ресурс КАК Значение
		|ИЗ
		|	(ВЫБРАТЬ
		|		ПоступлениеВКассахККМ.Организация КАК Организация,
		|		ПоступлениеВКассахККМ.КассаККМ КАК КассаККМ,
		|		ПоступлениеВКассахККМ.Регистратор КАК Регистратор,
		|		ПоступлениеВКассахККМ.СуммаОборот КАК Сумма
		|	ИЗ
		|		РегистрНакопления.ДенежныеСредстваВКассахККМ.Обороты(&ДатаНачала, &ДатаОкончания, Регистратор, ) КАК ПоступлениеВКассахККМ
		|	ГДЕ
		|		ТИПЗНАЧЕНИЯ(ПоступлениеВКассахККМ.Регистратор) = ТИП(Документ.ЧекККМ)) КАК Рег
		|ГДЕ
		|	&Отборы
		|СГРУППИРОВАТЬ ПО &Группировки";
	ИначеЕсли Стр.Ресурс="ВыбытиеНаличныхИзКассыККМ" Тогда
		ТекстЭлемента =  
		"ВЫБРАТЬ
		|	&ДляСравнения КАК ДляСравнения,
		|	&Индекс КАК Индекс,
		|	&Разделитель КАК Разделитель,
		|	&Ресурс КАК Значение
		|ИЗ
		|	(ВЫБРАТЬ
		|		ВыбытиеВКассахККМ.Организация КАК Организация,
		|		ВыбытиеВКассахККМ.КассаККМ КАК КассаККМ,
		|		ВыбытиеВКассахККМ.Регистратор КАК Регистратор,
		|		-ВыбытиеВКассахККМ.СуммаОборот КАК Сумма
		|	ИЗ
		|		РегистрНакопления.ДенежныеСредстваВКассахККМ.Обороты(&ДатаНачала, &ДатаОкончания, Регистратор, ) КАК ВыбытиеВКассахККМ
		|	ГДЕ
		|		ТИПЗНАЧЕНИЯ(ВыбытиеВКассахККМ.Регистратор) В (ТИП(Документ.ЧекККМВозврат), ТИП(Документ.ВыемкаНаличных))) КАК Рег
		|ГДЕ
		|	&Отборы
		|СГРУППИРОВАТЬ ПО &Группировки";
	ИначеЕсли Стр.Ресурс="ПолученоОплатКартой" ИЛИ Стр.Ресурс="ВозвратОплатКартой" Тогда
		ТекстЭлемента = 
		"ВЫБРАТЬ
		|	&ДляСравнения КАК ДляСравнения,
		|	&Индекс КАК Индекс,
		|	&Разделитель КАК Разделитель,
		|	&Ресурс КАК Значение
		|ИЗ
		|	(ВЫБРАТЬ
		|		ОплатаПлатежнымиКартами.Организация КАК Организация,
		|		ОплатаПлатежнымиКартами.ЭквайринговыйТерминал КАК ЭквайринговыйТерминал,
		|		ОплатаПлатежнымиКартами.СтруктурнаяЕдиница КАК СтруктурнаяЕдиница,
		|		ОплатаПлатежнымиКартами.ЭквайринговыйТерминал.Касса КАК КассаККМ,
		|		ОплатаПлатежнымиКартами.Регистратор КАК Регистратор,
		|		ВЫБОР
		|			КОГДА ОплатаПлатежнымиКартами.Сумма > 0
		|				ТОГДА ОплатаПлатежнымиКартами.Сумма
		|			ИНАЧЕ 0
		|		КОНЕЦ КАК Приход,
		|		ВЫБОР
		|			КОГДА ОплатаПлатежнымиКартами.Сумма < 0
		|				ТОГДА -ОплатаПлатежнымиКартами.Сумма
		|			ИНАЧЕ 0
		|		КОНЕЦ КАК Расход
		|	ИЗ
		|		РегистрНакопления.ОплатаПлатежнымиКартами КАК ОплатаПлатежнымиКартами
		|	ГДЕ
		|		ОплатаПлатежнымиКартами.Период МЕЖДУ &ДатаНачала И &ДатаОкончания) КАК Рег
		|ГДЕ
		|	&Отборы
		|СГРУППИРОВАТЬ ПО &Группировки";		
	ИначеЕсли Стр.Ресурс="ПередачаНаличныхВСейф" Тогда
		ТекстЭлемента =  
		"ВЫБРАТЬ
		|   &ДляСравнения КАК ДляСравнения, 
		|   &Индекс КАК Индекс, 
		|   &Разделитель КАК Разделитель, 
		|	&Ресурс КАК Значение
		|ИЗ
		|	РегистрНакопления.ДенежныеСредстваКПоступлению.Остатки(&Период, ) КАК Рег
		|ГДЕ &Отборы
		|СГРУППИРОВАТЬ ПО &Группировки";
	ИначеЕсли Стр.Ресурс="КоличествоЧековПредоплата" ИЛИ Стр.Ресурс="СуммаЧековПредоплата" Тогда
		ТекстЭлемента = 
		"ВЫБРАТЬ
		|	&ДляСравнения КАК ДляСравнения,
		|	&Индекс КАК Индекс,
		|	&Разделитель КАК Разделитель,
		|	&Ресурс КАК Значение
		|ИЗ
		|	(ВЫБРАТЬ
		|	ПредоплатаПоЧекам.Организация КАК Организация,
		|	ПредоплатаПоЧекам.ТипРасчетов КАК ТипРасчетов,
		|	ПредоплатаПоЧекам.Контрагент КАК Контрагент,
		|	ПредоплатаПоЧекам.Договор КАК Договор,
		|	ПредоплатаПоЧекам.Документ КАК Документ,
		|	ПредоплатаПоЧекам.Заказ КАК Заказ,
		|	ПредоплатаПоЧекам.СуммаРасход КАК Сумма
		|ИЗ
		|	РегистрНакопления.РасчетыСПокупателями.Обороты(
		|			&ДатаНачала,
		|			&ДатаОкончания,
		|			,
		|			ТипРасчетов = ЗНАЧЕНИЕ(Перечисление.ТипыРасчетов.Аванс)
		|				И ТИПЗНАЧЕНИЯ(Документ) В (ТИП(Документ.ЧекККМ))) КАК ПредоплатаПоЧекам) КАК Рег
		|ГДЕ
		|	&Отборы
		|СГРУППИРОВАТЬ ПО &Группировки";
	ИначеЕсли Стр.Ресурс="ВыдачаНаличныхИзСейфа" Тогда
		ТекстЭлемента = 
		"ВЫБРАТЬ
		|	&ДляСравнения КАК ДляСравнения,
		|	&Индекс КАК Индекс,
		|	&Разделитель КАК Разделитель,
		|	&Ресурс КАК Значение
		|ИЗ
		|	(ВЫБРАТЬ
		|		ВыбытиеДС.Организация КАК Организация,
		|		ВыбытиеДС.БанковскийСчетКасса КАК Касса,
		|		ВыбытиеДС.Регистратор КАК Регистратор,
		|		ВыбытиеДС.СуммаРасход КАК Сумма
		|	ИЗ
		|		РегистрНакопления.ДенежныеСредства.Обороты(&ДатаНачала, &ДатаОкончания, Регистратор, ТипДенежныхСредств = Значение(Перечисление.ТипыДенежныхСредств.Наличные)) КАК ВыбытиеДС
		|	ГДЕ
		|		ВЫРАЗИТЬ(ВыбытиеДС.Регистратор КАК Документ.РасходИзКассы).ВидОперации В (ЗНАЧЕНИЕ(Перечисление.ВидыОперацийРасходИзКассы.Покупателю), ЗНАЧЕНИЕ(Перечисление.ВидыОперацийРасходИзКассы.ПеремещениеВКассуККМ))) КАК Рег
		|ГДЕ
		|	&Отборы
		|СГРУППИРОВАТЬ ПО &Группировки";
	КонецЕсли; 
	
	ТекстЭлемента = СтрЗаменить(
	ТекстЭлемента,
	"&Ресурс",
	ТекстРесурса);
	ДобавитьЭлементВЗапросПоказатели(Запрос, ТекстЭлемента, Стр, Индекс, Параметры);
	
КонецПроцедуры

