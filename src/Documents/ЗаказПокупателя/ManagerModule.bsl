
&Перед("ДобавитьКомандыПечати")
Процедура am_ДобавитьКомандыПечати(КомандыПечати)
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.Идентификатор = "КоммерческоеПредложениеСФаксимиле";
	КомандаПечати.Представление = НСтр("ru = 'Коммерческое предложение (Факсимиле)'");
	КомандаПечати.СписокФорм = "ФормаДокумента,ФормаСписка";
	КомандаПечати.ПроверкаПроведенияПередПечатью = Ложь;
	КомандаПечати.МестоРазмещения = "ПодменюПечатьФаксимиле";
	КомандаПечати.Порядок = 99;
КонецПроцедуры

&После("Печать")
Процедура am_Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода)
	ПечатнаяФорма = УправлениеПечатью.СведенияОПечатнойФорме(КоллекцияПечатныхФорм, "КоммерческоеПредложениеСФаксимиле");
	Если ПечатнаяФорма <> Неопределено Тогда		
		ПечатнаяФорма.ТабличныйДокумент = Новый ТабличныйДокумент;
		ПечатнаяФорма.ТабличныйДокумент.КлючПараметровПечати = "ПАРАМЕТРЫ_ПЕЧАТИ_ЗаказПокупателя_КоммерческоеПредложение";
		ПечатнаяФорма.ПолныйПутьКМакету = "Документ.ЗаказПокупателя.ПФ_MXL_КоммерческоеПредложениеСФаксимиле";
		ПечатнаяФорма.СинонимМакета = НСтр("ru ='Коммерческое предложение'");
		
		Ошибки = Новый Массив;
		
		СформироватьКоммерческоеПредложение(ПечатнаяФорма, МассивОбъектов, ОбъектыПечати, Ошибки);		
		
		ВывестиПодписьФаксимиле(ПечатнаяФорма);	
	КонецЕсли;
КонецПроцедуры

Процедура ВывестиПодписьФаксимиле(ПечатнаяФорма)
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ ПЕРВЫЕ 1
	               |	Организации.ФайлФаксимильнаяПечать КАК ФайлФаксимильнаяПечать,
	               |	Подписи.РасшифровкаПодписи КАК РасшифровкаПодписи,
	               |	Подписи.Факсимиле КАК Факсимиле
	               |ИЗ
	               |	Справочник.Подписи КАК Подписи
	               |		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Организации КАК Организации
	               |		ПО Подписи.Организация = Организации.Ссылка
	               |ГДЕ
	               |	Организации.Ссылка = &Организация";
	Запрос.УстановитьПараметр("Организация", ПолучитьОрганизацию());
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если НЕ Выборка.Следующий() Тогда
		Возврат;		
	КонецЕсли;
	
	ФайлПечать = Выборка.ФайлФаксимильнаяПечать;
	ФайлПодпись = Выборка.Факсимиле;
	РасшифровкаПодписи = Выборка.РасшифровкаПодписи;
	
	ТабличныйДокумент = ПечатнаяФорма.ТабличныйДокумент;
	Макет = УправлениеПечатью.МакетПечатнойФормы(ПечатнаяФорма.ПолныйПутьКМакету);
	
	ОбластьФаксимиле = Макет.ПолучитьОбласть("Факсимиле");
	
	ПечатьФаксимиле = ПолучитьДвоичныеДанныеКартинку(ФайлПечать);
	ПодписьФаксимиле = ПолучитьДвоичныеДанныеКартинку(ФайлПодпись);
	
	ОбластьФаксимиле.Рисунки.D1.Картинка = ПодписьФаксимиле;
	ОбластьФаксимиле.Рисунки.D2.Картинка = ПечатьФаксимиле;
	ОбластьФаксимиле.Параметры.РасшифровкаПодписи = РасшифровкаПодписи;
	
	ТабличныйДокумент.Вывести(ОбластьФаксимиле);
КонецПроцедуры

Функция ПолучитьОрганизацию()
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ РАЗРЕШЕННЫЕ ПЕРВЫЕ 1
	               |	НастройкиПользователей.Настройка КАК Настройка,
	               |	НастройкиПользователей.Значение КАК Значение
	               |ИЗ
	               |	РегистрСведений.НастройкиПользователей КАК НастройкиПользователей
	               |ГДЕ
	               |	НастройкиПользователей.Настройка.Наименование = ""Основная организация""";

	Выборка = Запрос.Выполнить().Выбрать();
	Организация = Справочники.Организации.ПустаяСсылка();
	Если Выборка.Следующий() Тогда
		Организация = Выборка.Значение;
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(Организация) Тогда
		Организация = Справочники.Организации.ОрганизацияПоУмолчанию();	
	КонецЕсли;	
	
	Возврат Организация;
КонецФункции

Функция ПолучитьДвоичныеДанныеКартинку(ФайлКартинки)

	ДвоичныеДанныеФайла = РаботаСФайлами.ДвоичныеДанныеФайла(ФайлКартинки);
	Если ТипЗнч(ДвоичныеДанныеФайла) = Тип("Картинка") Тогда
		Картинка = ДвоичныеДанныеФайла;
	ИначеЕсли ТипЗнч(ДвоичныеДанныеФайла) = Тип("ДвоичныеДанные") Тогда
		Картинка = Новый Картинка(ДвоичныеДанныеФайла);
	Иначе
		Картинка = Новый Картинка;
	КонецЕсли;	
	Возврат Картинка;

КонецФункции	